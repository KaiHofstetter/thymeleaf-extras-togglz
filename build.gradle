buildscript {
	repositories {
		mavenLocal()
		maven {
			credentials {
				username = "${artifactoryUsername}"
				password = "${artifactoryPassword}"
			}
			url "${artifactoryRepoUrl}"
		}
	}
	dependencies {
		classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.0.17')
	}
}

group = 'com.github.heneke.thymeleaf'
version = '1.0.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'artifactory'
apply plugin: 'signing'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

afterEvaluate {
	project.archivesBaseName = 'thymeleaf-extras-togglz'
}

repositories { 
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile "org.thymeleaf:thymeleaf:2.1.3.RELEASE"
	compile "org.togglz:togglz-core:2.0.1.Final"
	testCompile "junit:junit:4.8.2"
	testCompile "org.togglz:togglz-testing:2.0.1.Final"
	testCompile "org.thymeleaf:thymeleaf-testing:2.1.3.RELEASE"
}

artifactory {
	publish {
		contextUrl = "${artifactoryUrl}"
		repository {
			if(project.version.endsWith('-SNAPSHOT')) {
				repoKey = 'pixw-snapshot-local'
			}
			else {
				repoKey = 'pixw-release-local'
			}
			username = "${artifactoryUsername}"
			password = "${artifactoryPassword}"
		}
	}
}
[install, uploadArchives, artifactoryPublish].each {
	it.dependsOn 'test'
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { deployment -> signing.signPom(deployment) }
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
		}
	}
}

[install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer].each {
	configure(it) {
		pom.project {
			packaging 'jar'
			name 'Thymeleaf Extras Togglz'
			description 'Thymeleaf dialect integrating Togglz feature toggles.'
			inceptionYear '2013'
			url 'https://github.com/heneke/thymeleaf-extras-togglz'
			scm {
				connection 'https://github.com/heneke/thymeleaf-extras-togglz.git'
				developerConnection 'https://github.com/heneke/thymeleaf-extras-togglz.git'
				url 'https://github.com/heneke/thymeleaf-extras-togglz/tree/master'
			}
			licenses {
				license {
					name 'The Apache License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				}
			}
			developers {
				developer {
					id 'heneke'
					name 'Hendrik Heneke'
					email 'heneke@gmail.com'
				}
			}
			issueManagement {
				url 'https://github.com/heneke/thymeleaf-extras-togglz/issues'
				system 'Github Issues'
			}
		}
	}
}

configure(signing) {
	sign configurations.archives
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

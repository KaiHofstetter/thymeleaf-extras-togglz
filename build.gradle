buildscript {
	repositories {
		mavenLocal()
		maven {
			credentials {
				username = "${artifactoryUsername}"
				password = "${artifactoryPassword}"
			}
			url "${artifactoryRepoUrl}"
		}
	}
	dependencies {
		classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.0.17')
	}
}

group = 'org.thymeleaf.extras'
version = '1.0.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'artifactory'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

afterEvaluate {
	project.archivesBaseName = 'thymeleaf-extras-togglz'
}

repositories { 
	mavenLocal()
	mavenCentral()
}

dependencies {
	compile "org.thymeleaf:thymeleaf:2.0.21"
	compile "org.togglz:togglz-core:2.0.1.Final"
	testCompile "junit:junit:4.8.2"
	testCompile "org.togglz:togglz-testing:2.0.1.Final"
	testCompile "org.thymeleaf:thymeleaf-testing:2.0.9"
}

artifactory {
	publish {
		contextUrl = "${artifactoryUrl}"
		repository {
			if(project.version.endsWith('-SNAPSHOT')) {
				repoKey = 'pixw-snapshot-local'
			}
			else {
				repoKey = 'pixw-release-local'
			}
			username = "${artifactoryUsername}"
			password = "${artifactoryPassword}"
		}
	}
}
artifactoryPublish.dependsOn 'test'

configure(install.repositories.mavenInstaller) {
	pom.project {
		packaging 'jar'
		name 'Thymeleaf Extras Togglz'
		description 'Thymeleaf dialect integrating Togglz feature toggles.'
		inceptionYear '2013'
		url 'https://github.com/heneke/thymeleaf-extras-togglz'
		scm {
			connection 'https://github.com/heneke/thymeleaf-extras-togglz.git'
			developerConnection 'https://github.com/heneke/thymeleaf-extras-togglz.git'
			url 'https://github.com/heneke/thymeleaf-extras-togglz/tree/master'
		}
		licenses {
			license {
				name 'The Apache License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			}
		}
		developers {
			developer {
				id 'heneke'
				name 'Hendrik Heneke'
				email 'heneke@gmail.com'
			}
		}
		issueManagement {
			url 'https://github.com/heneke/thymeleaf-extras-togglz/issues'
			system 'Github Issues'
		}
	}
}
